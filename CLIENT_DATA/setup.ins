

[Actions]
requiredWinstVersion >= "4.11.2.1"
setloglevel="7"
comment "define variables"
sub_define_vars

if CompareDotSeparatedNumbers(getMsVersionInfo, "<", "6.0")
	LogError "This is for NT 6 (Vista) and above"
	isFatalError
else

Message=Windows config ......
ShowBitMap "%ScriptPath%\config-win-base.png" "Win Config"

set $INST_NTVersion$ = GetMsVersionInfo

comment "Set Values"
sub_get_properties

comment "do patches"

;http://support.microsoft.com/?scid=kb%3Ben-us%3B947709&x=11&y=14
if $Flag_xp_firewall$ = "on"
	if ($INST_NTVersion$ >= "6")
		DosInAnIcon_nt6_firewall_on winst /sysnative
	else
		DosInAnIcon_nt5_firewall_on
	endif
else
	if ($INST_NTVersion$ >= "6")
		DosInAnIcon_nt6_firewall_off winst /sysnative
	else
		DosInAnIcon_nt5_firewall_off
	endif
endif


; http://support.microsoft.com/default.aspx?scid=kb;en-us;823732
if $Flag_usb$ = "on"
	Registry_usb_on /sysnative
else
	Registry_usb_off /sysnative
endif


if ($Flag_NoWindowsTour$ = "1")
	Registry_NoWindowsTour /sysnative
else
	Registry_WindowsTour /sysnative
endif
 
if ($Flag_NoErrorReport$ = "1")
	Registry_NoErrorReport /sysnative
else
	Registry_ErrorReport /sysnative
endif

if ($Flag_NoAutorunCD$ = "1")
	Registry_NoAutorunCD /sysnative
else
	Registry_AutorunCD /sysnative
endif

if ($Flag_NoBalloons$ = "1")
	Registry_NoBalloons /sysnative
else
	Registry_Balloons /sysnative
endif


if ($Flag_WaitForNetwork$ = "1")
	Registry_WaitForNetwork /sysnative
else
	Registry_NoWaitForNetwork /sysnative
endif

if not ($Flag_i386PathC$ = "0")
	Registry_set_i386_source /sysnative
endif
 
if ($Flag_allowremoterpc$ = "1")
	Registry_AllowRPC /sysnative
else
	Registry_NoAllowRPC /sysnative
endif

if ($Flag_disable_remote_uac$ = "true")
	Registry_AllowRemoteExecution /sysnative
else
	Registry_NoAllowRemoteExecution /sysnative
endif
 
 
if ($Flag_autologin$ = "true")
	Registry_AllowAutoLogin /sysnative
else
	Registry_NoAllowAutoLogin /sysnative
endif


comment "Patch the local Profiles ..."
ProfileActions

if ($Flag_reboot$ = "true")
	ExitWindows /Reboot
endif	

; endif on windows 7 and above...
endif

[ProfileActions]
DefVar $doprofile$
DefVar $force_login_script$

comment "The profile part ..."
set $doprofile$ = "true"

if GetScriptMode = "Login"
	comment "we are at login ..."
	if getValue("installationstate", getProductMap) = "installed"
		comment "Product is installed"
		comment "should loginscript run on every login"
		Set $force_login_script$ = GetProductProperty("force_login_script", "off")
		comment "Did we run this script before ? - and set version stamp in profile"
		if scriptWasExecutedBefore and ($force_login_script$ = "off")
			comment "loginscript did run before - nothing to do"
			set $doprofile$ = "false"
		endif
	else
		comment "Product is not installed - so we do not patch the profile"
		set $doprofile$ = "false"
	endif
	if $doprofile$ = "true"
		Message=Windows config ......
		ShowBitMap "%ScriptPath%\winconfig.png" "Win Config"
		sub_define_vars
		sub_get_properties
	endif
endif

if $doprofile$ = "true"
	comment "do patches"
	
	if $Flag_ScreenSaveActive$ = "on"
		Registry_on_ScreenSaveActive /sysnative
	else
		Registry_off_ScreenSaveActive /sysnative
	endif
	
	if ($Flag_NoWindowsTour$ = "1")
		Registry_HKCU_NoWindowsTour /sysnative
	else
		Registry_HKCU_WindowsTour /sysnative
	endif
	
	if ($Flag_NoBalloons$ = "1")
		Registry_HKCU_NoBalloons /sysnative
	else
		Registry_HKCU_Balloons /sysnative
	endif
	
	if ($Flag_ClassicStartMenu$ = "1")
		Registry_HKCU_ClassicStartMenu /sysnative
	else
		Registry_HKCU_NoClassicStartMenu /sysnative
	endif
	
	if ($Flag_ClassicControl$ = "1")
		Registry_HKCU_Flag_ClassicControl /sysnative
	else
		Registry_HKCU_Flag_NoClassicControl /sysnative
	endif
	
	if ($Flag_NumlockOn$ = "0")
		Registry_HKCU_NumlockOn /sysnative
	else
		Registry_HKCU_NumlockOff /sysnative
	endif
	
	if ($Flag_Explorer$ = "1")
		Registry_HKCU_ExplorerConfig /sysnative
	else
		Registry_HKCU_ExplorerConfig /sysnative
	endif
	
	if ($Flag_Explorer_showsystemfiles$ = "1")
		Registry_HKCU_ExplorerShowSystemFiles /sysnative
	else
		Registry_HKCU_ExplorerNoSystemFiles /sysnative
	endif
endif ; $doprofile$ = "true"

[sub_define_vars]
DefVar $ParmName$
DefVar $ParmValue$
DefVar $DefaultValue$
DefVar $INST_NTVersion$


;*** Flag if patch shall be executed
;*** 0 = do not execute patch
;*** 1 = execute patch

DefVar $Flag_NoWindowsTour$
DefVar $Flag_NoErrorReport$
DefVar $Flag_NoAutorunCD$
DefVar $Flag_NoBalloons$
DefVar $Flag_ClassicStartMenu$
DefVar $Flag_ClassicControl$
DefVar $Flag_NumlockOn$
DefVar $Flag_Explorer$
DefVar $Flag_Explorer_showsystemfiles$
DefVar $Flag_WaitForNetwork$
DefVar $Flag_i386PathC$
DefVar $Flag_ScreenSaveActive$
DefVar $Flag_xp_firewall$
DefVar $Flag_usb$
DefVar $Flag_reboot$
DefVar $Flag_autologin$
DefVar $Val_default_password$
DefVar $Val_default_account$
DefVar $Val_default_domain$



;*** Values for Explorer Settings
;*** Achtung, Settings gelten nur f√ºr bereits vorhandene Benutzer!

DefVar $Val_Explorer_ClassicViewState$
DefVar $Val_Explorer_HideFileExt$
;DefVar $Val_Explorer_SuperHidden$
;DefVar $Val_Explorer_ShowSuperHidden$
;DefVar $Val_Explorer_WebViewBarricade$
DefVar $Val_Explorer_Hidden$
DefVar $Val_Explorer_WebView$
DefVar $Flag_allowremoterpc$
DefVar $Flag_disable_remote_uac$


[sub_get_properties]
comment "Flag_ScreenSaveActive"
comment "description: Activate screen saver"
Set $Flag_ScreenSaveActive$ = GetProductProperty("Flag_ScreenSaveActive", "off")

comment "Flag_xp_firewall"
comment "description: Enable xp firewall"
Set $Flag_xp_firewall$ = GetProductProperty("Flag_xp_firewall", "on")

comment "Flag_usb"
comment "description: Enable use of USB Storage Devices"
Set $Flag_usb$ = GetProductProperty("Flag_usb", "on")

comment "Flag_NoWindowsTour"
comment "description: No Windows Tour"
Set $Flag_NoWindowsTour$ = GetProductProperty("Flag_NoWindowsTour", "1")

comment "Flag_NoErrorReport"
comment "description: No error report to Microsoft"
Set $Flag_NoErrorReport$ = GetProductProperty("Flag_NoErrorReport", "1")

comment "Flag_NoAutorunCD"
comment "description: No CD Autorun"
Set $Flag_NoAutorunCD$ = GetProductProperty("Flag_NoAutorunCD", "1")

comment "Flag_NoBalloons"
comment "description: No Hints (Sprechblasen)"
Set $Flag_NoBalloons$ = GetProductProperty("Flag_NoBalloons", "1")

comment "Flag_ClassicStartMenu"
comment "description: Classic Start Menu"
Set $Flag_ClassicStartMenu$ = GetProductProperty("Flag_ClassicStartMenu", "1")

comment "Flag_ClassicControl"
comment "description: Klassische Systemsteuerung"
Set $Flag_ClassicControl$ = GetProductProperty("Flag_ClassicControl", "1")

comment "Flag_NumlockOn"
comment "description: Numlock On"
Set $Flag_NumlockOn$ = GetProductProperty("Flag_NumlockOn", "0")

comment "Flag_Explorer"
comment "description: Masterflag: Do Explorer Settings"
Set $Flag_Explorer$ = GetProductProperty("Flag_Explorer", "1")

comment "Flag_Explorer_showsystemfiles"
comment "description: Show protected system files and directories"
Set $Flag_Explorer_showsystemfiles$ = GetProductProperty("Flag_Explorer_showsystemfiles", "1")

comment "Flag_i386PathC"
comment "description: Suchpfad fuer I386 auf anderes Laufwerk setzen"
Set $Flag_i386PathC$ = GetProductProperty("Flag_i386PathC", "0")

comment "Val_Explorer_ClassicViewState"
comment "description: Ansicht nicht fuer jeden Ordner einzeln speichern"
Set $Val_Explorer_ClassicViewState$ = GetProductProperty("Val_Explorer_ClassicViewState", "1")

comment "Val_Explorer_HideFileExt"
comment "description: hide known file extensions"
Set $Val_Explorer_HideFileExt$ = GetProductProperty("Val_Explorer_HideFileExt", "0")

comment "Val_Explorer_Hidden"
comment "description: 1=alle Dateien anzeigen  2=versteckte ausblenden"
Set $Val_Explorer_Hidden$ = GetProductProperty("Val_Explorer_Hidden", "1")

comment "Val_Explorer_WebView"
comment "description: use web view"
Set $Val_Explorer_WebView$ = GetProductProperty("Val_Explorer_WebView", "0")

	;comment "Val_Explorer_SuperHidden"
	;comment "description:  1=geschuetzte Systemdateien und Systemordner anzeigen (I)"
	;Set $Val_Explorer_SuperHidden$ = GetProductProperty("Val_Explorer_SuperHidden", "0")

	;comment "Val_Explorer_ShowSuperHidden"
	;comment "description:  1=geschuetzte Systemdateien und Systemordner anzeigen (II)"
	;Set $Val_Explorer_ShowSuperHidden$ = GetProductProperty("Val_Explorer_ShowSuperHidden", "0")

	;comment "Val_Explorer_WebViewBarricade"
	;comment "description:  1=geschuetzte Systemdateien und Systemordner anzeigen (III)"
	;Set $Val_Explorer_WebViewBarricade$ = GetProductProperty("Val_Explorer_WebViewBarricade", "0")

comment "Flag_WaitForNetwork"
comment "description: Beim Start auf Netzwerk warten"
Set $Flag_WaitForNetwork$ = GetProductProperty("Flag_WaitForNetwork", "1")

comment "Flag_i386PathC"
comment "description: Suchpfad fuer I386 auf C: setzen"
Set $Flag_i386PathC$ = GetProductProperty("Flag_i386PathC", "0")

comment "flag_allowremoterpc"
comment "description: Allow remote procedures to run on this computer"
Set $Flag_allowremoterpc$ = GetProductProperty("flag_allowremoterpc", "1")

comment "flag_disable_remote_uac"
comment "description: Disable remote UAC to permit remote execution on this computer"
Set $Flag_disable_remote_uac$ = GetProductProperty("flag_disable_remote_uac", "1")

comment "flag_reboot"
comment "description: Force a reboot of this computer after applying changes"
Set $Flag_reboot$ = GetProductProperty("reboot", "0")

comment "flag_autologin"
comment "description: enable/disable auto login"
Set $Flag_autologin$ = GetProductProperty("flag_autologin", "0")

comment "val_default_password"
comment "description: default password to use with auto login"
Set $Val_default_password$ = GetProductProperty("val_default_password", "password")

comment "val_default_account"
comment "description: default account to use with auto login"
Set $Val_default_account$ = GetProductProperty("val_default_account", "dad")

comment "val_default_domain"
comment "description: default domain to use with auto login"
Set $Val_default_domain$ = GetProductProperty("val_default_domain", "WIN7-2-PC")



;***********************
;**    misc	   ***
;***********************

[DosInAnIcon_nt5_firewall_on]
netsh firewall set opmode enable

[DosInAnIcon_nt5_firewall_off]
netsh firewall set opmode disable

[DosInAnIcon_nt6_firewall_on]
netsh advfirewall set currentprofile state on

[DosInAnIcon_nt6_firewall_off]
netsh advfirewall set currentprofile state off

;***********************
;**    SUB-Sektion   ***
;***********************


;***********************
;**    HKLM-Eintr√§ge ***
;***********************

;** Windows-Tour abschalten
[Registry_NoWindowsTour]
openKey [HKEY_LOCAL_MACHINE\Software\Microsoft\Windows\CurrentVersion\Applets\Tour]
set "RunCount" = REG_DWORD:00000000

[Registry_WindowsTour]
openKey [HKEY_LOCAL_MACHINE\Software\Microsoft\Windows\CurrentVersion\Applets\Tour]
set "RunCount" = REG_DWORD:00000001

;** keine Fehlerberichte an Microsoft
[Registry_NoErrorReport]
openKey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\PCHealth\ErrorReporting]
set "DoReport" = REG_DWORD:00000000
set "ShowUI"   = REG_DWORD:00000000

[Registry_ErrorReport]
openKey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\PCHealth\ErrorReporting]
set "DoReport" = REG_DWORD:00000001
; ?
set "ShowUI"   = REG_DWORD:00000001

;** Microsoft-Online-√úberpr√ºfung bei Neustart mit Internet verhindern
[Registry_RegDone]
openkey [HKEY_LOCAL_MACHINE\Software\Microsoft\Windows NT\CurrentVersion]
set "RegDone" = "1"

;** CD Autorun abschalten
[Registry_NoAutorunCD]
openkey [HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Services\CDRom]
set "AutoRun" = REG_DWORD:00000000

[Registry_AutorunCD]
openkey [HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Services\CDRom]
set "AutoRun" = REG_DWORD:00000001

;** keine Tooltipp-Animation (Sprechblasen)
[Registry_NoBalloons]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\VisualEffects\TooltipAnimation]
set "CheckedValue" = REG_DWORD:0000
;den Key habe ich weder auf xp noch win7 gefunden
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\Advanced]
set "EnableBalloonTips" = REG_DWORD:00000000

[Registry_Balloons]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\VisualEffects\TooltipAnimation]
set "CheckedValue" = REG_DWORD:0001
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\Advanced]
set "EnableBalloonTips" = REG_DWORD:00000001

[Registry_set_i386_source]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Setup]
set "SourcePath" = "$Flag_i386PathC$"
set "ServicePackSourcePath" = "$Flag_i386PathC$"
set "CDInstall" = reg_dword:0
; (auf Win7 kein i386 Verzeichnis)

[Registry_WaitForNetwork]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Policies\Microsoft\WindowsNT\CurrentVersion\Winlogon]
set "SyncForegroundPolicy" = reg_dword:1

[Registry_NoWaitForNetwork]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Policies\Microsoft\WindowsNT\CurrentVersion\Winlogon]
set "SyncForegroundPolicy" = reg_dword:0


[Registry_AllowRPC]
openkey [HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Terminal Server]
set "AllowRemoteRPC" = REG_DWORD:00000001

[Registry_NoAllowRPC]
openkey [HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Terminal Server]
set "AllowRemoteRPC" = REG_DWORD:00000000


[Registry_AllowRemoteExecution]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Policies\System]
set "LocalAccountTokenFilterPolicy" = REG_DWORD:00000001

[Registry_NoAllowRemoteExecution]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Policies\System]
set "LocalAccountTokenFilterPolicy" = REG_DWORD:00000000


[Registry_AllowAutoLogin]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Winlogon]
set "AutoAdminLogon" = REG_SZ:"1"
set "DefaultUserName" = REG_EXPAND_SZ:"$Val_default_account$"
set "DefaultPassword" = REG_EXPAND_SZ:"$Val_default_password$"
set "DefaultDomainName" = REG_EXPAND_SZ:"$Val_default_domain$"

deletekey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Winlogon\AutoLogonCount]
deletekey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Winlogon\AutoLogonChecked]


[Registry_NoAllowAutoLogin]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Winlogon]
set "AutoAdminLogon" = REG_SZ:"0"



;***********************
;**    HKCU-Eintr√§ge ***
;***********************


[Registry_on_ScreenSaveActive]
openKey [HKCU\Control Panel\Desktop]
set "ScreenSaveActive"=REG_SZ:"1"

[Registry_off_ScreenSaveActive]
openKey [HKCU\Control Panel\Desktop]
set "ScreenSaveActive"=REG_SZ:"0"

;** keine Windows Tour
[Registry_HKCU_NoWindowsTour]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Applets\Tour]
set "RunCount"=REG_DWORD:00000000

[Registry_HKCU_WindowsTour]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Applets\Tour]
set "RunCount"=REG_DWORD:00000001

;** Klassisches Startmen√º
[Registry_HKCU_ClassicStartMenu]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Policies\Explorer]
set "NoSimpleStartMenu" = REG_DWORD:00000001

[Registry_HKCU_NoClassicStartMenu]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Policies\Explorer]
set "NoSimpleStartMenu" = REG_DWORD:00000000

;** Klassische Systemsteuerung
[Registry_HKCU_Flag_ClassicControl]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Policies\Explorer]
set "ForceClassicControlPanel" = REG_DWORD:00000001

[Registry_HKCU_Flag_NoClassicControl]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Policies\Explorer]
set "ForceClassicControlPanel" = REG_DWORD:00000000

;** Numlock aktivieren
[Registry_HKCU_NumlockOn]
openKey [HKCU\Control Panel\Keyboard]
set "InitialKeyboardIndicators"="2"

[Registry_HKCU_NumlockOff]
openKey [HKCU\Control Panel\Keyboard]
set "InitialKeyboardIndicators"="0"

;** Explorer Konfiguration
[Registry_HKCU_ExplorerConfig]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\Advanced]
Set "ClassicViewState" = REG_DWORD:$Val_Explorer_ClassicViewState$
Set "HideFileExt"      = REG_DWORD:$Val_Explorer_HideFileExt$
Set "Hidden"           = REG_DWORD:$Val_Explorer_Hidden$
Set "WebView"          = REG_DWORD:$Val_Explorer_WebView$

;** Explorer Show System Files and Directories
[Registry_HKCU_ExplorerShowSystemFiles]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\Advanced]
Set "SuperHidden"      = REG_DWORD:00000001
Set "ShowSuperHidden"  = REG_DWORD:00000001
Set "WebViewBarricade" = REG_DWORD:00000001

[Registry_HKCU_ExplorerNoSystemFiles]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\Advanced]
Set "SuperHidden"      = REG_DWORD:00000000
Set "ShowSuperHidden"  = REG_DWORD:00000000
Set "WebViewBarricade" = REG_DWORD:00000000


;** No Balloons
[Registry_HKCU_NoBalloons]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\Advanced]
set "EnableBalloonTips"=REG_DWORD:00000000
set "FolderContentsInfoTip"=REG_DWORD:00000000
set "StartButtonBalloonTip"=REG_DWORD:00000000
set "ShowInfoTip"=REG_DWORD:00000000
openkey [HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\tips]
set "Show"=REG_DWORD:00000000
openkey [HKCU\Software\Microsoft\Windows\CurrentVersion\Policies\Explorer]
set "NoSMBalloonTip"=REG_DWORD:00000000

[Registry_HKCU_Balloons]
openKey [HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\Advanced]
set "EnableBalloonTips"=REG_DWORD:00000001
set "FolderContentsInfoTip"=REG_DWORD:00000001
set "StartButtonBalloonTip"=REG_DWORD:00000001
set "ShowInfoTip"=REG_DWORD:00000001
openkey [HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\tips]
set "Show"=REG_DWORD:00000001
openkey [HKCU\Software\Microsoft\Windows\CurrentVersion\Policies\Explorer]
set "NoSMBalloonTip"=REG_DWORD:00000001

[Registry_usb_off]
openkey [HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\usbstor]
set "start"= REG_DWORD:4

[Registry_usb_on]
openkey [HKEY_LOCAL_MACHINE\System\CurrentControlSet\Services\usbstor]
set "start"= REG_DWORD:3


